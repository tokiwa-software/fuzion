# This file is part of the Fuzion language implementation.
#
# The Fuzion language implementation is free software: you can redistribute it
# and/or modify it under the terms of the GNU General Public License as published
# by the Free Software Foundation, version 3 of the License.
#
# The Fuzion language implementation is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
# License for more details.
#
# You should have received a copy of the GNU General Public License along with The
# Fuzion language implementation.  If not, see <https://www.gnu.org/licenses/>.


# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion standard library feature i16
#
#  Author: Fridtjof Siebert (siebert@tokiwa.software)
#
# -----------------------------------------------------------------------

# i16 -- 16-bit signed integer values
#
i16(val i16) : wrapping_integer, has_interval is

  thiz => i16.this.val

  # overflow checking

  # would negation -thiz cause an overflow?
  redef wrapped_on_neg => is_min

  # would addition thiz + other cause an overflow or underflow?
  fixed overflow_on_add (other i16) => thiz > i16.type.zero && i16.type.max -° thiz < other
  fixed underflow_on_add(other i16) => thiz < i16.type.zero && i16.type.min -° thiz > other

  # would subtraction thiz - other cause an overflow or underflow?
  fixed overflow_on_sub (other i16) => thiz > i16.type.zero && thiz -° i16.type.max > other
  fixed underflow_on_sub(other i16) => thiz < i16.type.zero && thiz -° i16.type.min < other

  # would multiplication thiz * other cause an overflow or underflow?
  fixed overflow_on_mul (other i16) => as_i32 *° other.as_i32 > i16.type.max.as_i32
  fixed underflow_on_mul(other i16) => as_i32 *° other.as_i32 < i16.type.min.as_i32

  # neg, add, sub, mul with wrap-around semantics
  fixed prefix -° i16 is intrinsic
  fixed infix +° (other i16) i16 is intrinsic
  fixed infix -° (other i16) i16 is intrinsic
  fixed infix *° (other i16) i16 is intrinsic

  # division and remainder with check for div-by-zero
  fixed infix / (other i16)
    pre
      safety: other != i16.type.zero
   => div(other)
  fixed infix % (other i16)
    pre
      safety: other != i16.type.zero
   => mod(other)

  # private division and remainder with crash in case of div-by-zero
  private div (other i16) i16 is intrinsic
  private mod (other i16) i16 is intrinsic

  # bitwise and, or and xor operations
  fixed infix &  (other i16) i16 is intrinsic
  fixed infix |  (other i16) i16 is intrinsic
  fixed infix ^  (other i16) i16 is intrinsic

  # shift operations (signed)
  fixed infix >> (other i16) i16 is intrinsic
  fixed infix << (other i16) i16 is intrinsic

  # conversion to u32, i64 and u64, with range check
  as_i8 i8
    pre
      thiz ≥ i8.type.min.as_i16,
      thiz ≤ i8.type.max.as_i16
    is
      low8bits.cast_to_i8
  as_i32 i32 is intrinsic
  as_i64  => thiz.as_i32.as_i64
# as_i128 => thiz.as_i32.as_i128

  as_u8 u8
    pre
      debug: (thiz ≥ i16.type.zero) && (thiz ≤ u8.type.max.as_i16)
    is
      low8bits
  as_u16 u16
    pre
      debug: thiz ≥ i16.type.zero
    is
      cast_to_u16
  as_u32 u32
    pre
      debug: thiz ≥ i16.type.zero
    is
      cast_to_u32
  as_u64 u64
    pre
      debug: thiz ≥ i16.type.zero
    is
      cast_to_u64
  as_u128 u128
    pre
      debug: thiz ≥ i16.type.zero
    is
      cast_to_u128
  as_int int is
    int as_i64

  # casting to unsigned, adding 1<<16 if negative
  low8bits    u8   is cast_to_u16.as_u8      # 0x1234 ->   0x34, -0x1234 -> 0xcc
  cast_to_u16  u16  is intrinsic             # 0x1234 -> 0x1234, -0x1234 -> 0xedcc
  cast_to_u32  u32  is cast_to_u16.as_u32    # 0x1234 -> 0x1234, -0x1234 -> 0x0000_edcc
  cast_to_u64  u64  is cast_to_u16.as_u64    # 0x1234 -> 0x1234, -0x1234 -> 0x0000_0000_0000_edcc
  cast_to_u128 u128 is cast_to_u16.as_u128   # 0x1234 -> 0x1234, -0x1234 -> 0x0000_0000_0000_0000_0000_0000_0000_edcc

  # NYI: max is redefined here only to solve repeated inheritance conflict. Since max inherited
  # from has_interval<i8> is abstract, fz should not complain about this conflict.
  fixed redef max => i16 0x7fff


  # create hash code from this number
  hash u64 is
    cast_to_u64.hash


  # find the highest 1 bit in this integer and return integer with
  # this single bit set or 0 if this is zero.
  #
  highest_one_bit i16 is
    val.cast_to_u16.highest_one_bit.cast_to_i16


  # count the number of trailing zeros in this integer.
  #
  trailing_zeros i32 is
    val.cast_to_u16.trailing_zeros


  # count the number of 1 bits in the binary representation of this
  # integer.
  #
  ones_count i32 is
    val.cast_to_u16.ones_count


  # -----------------------------------------------------------------------
  #
  # type features:


  # identity element for 'infix +'
  #
  fixed type.zero i16 is 0


  # identity element for 'infix *'
  #
  fixed type.one  i16 is 1


  # equality
  #
  fixed type.equality(a, b i16) bool is intrinsic


  # total order
  #
  fixed type.lteq(a, b i16) bool is intrinsic


  # returns the number in whose bit representation all bits are ones
  fixed redef type.all_bits_set => i16 -1


  # minimum
  #
  fixed type.min => i16 -0x8000


  # maximum
  #
  fixed type.max => i16 0x7fff
