# This file is part of the Fuzion language implementation.
#
# The Fuzion language implementation is free software: you can redistribute it
# and/or modify it under the terms of the GNU General Public License as published
# by the Free Software Foundation, version 3 of the License.
#
# The Fuzion language implementation is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
# License for more details.
#
# You should have received a copy of the GNU General Public License along with The
# Fuzion language implementation.  If not, see <https://www.gnu.org/licenses/>.


# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion standard library feature mut
#
#  Author: Fridtjof Siebert (siebert@tokiwa.software)
#
# -----------------------------------------------------------------------

# mutate -- an effect that permits creation and mutation of mutable values.
#
# This effect is typically used to work with mutable values. You can create
# a mutable value as follows
#
#   v := mutate.env.new i32 42
#
# and then modify it using
#
#   v <- 666
#
# To read it, call 'get' as in
#
#   say "v is {v.get}"
#
# Convenience feature 'mut' and type inference allow the creation to be
# written as
#
#   v := mut 42
#
# NYI: syntax sugar to read mutable field using
#
#   w := v + 1
#
# instead of
#
#   w := v.get + 1
#
# is not supported yet.
#
public mutate : simple_effect is


  # does this effect support abort?
  public redef abortable => false


  # short-hand to access effect type
  #
  # NYI: This does not work yet
  #
  # M := mutate.this.type

  # an id used for runtime checks to verify that mutation made with the same effect
  # the mutable value was created with
  #
  id := fuzion.sys.misc.unique_id


  # panic operation to be used in case of a severe problem.  This is redefined within the
  # base lib to make sure fundamental uses of local mutability do not cause dependency
  # on the panic effect.
  #
  mpanic(msg String) => panic msg


  # common type for mutable data
  #
  private:public mutable_element is

    # id used to verify that mutation made with the same effect
    # the mutable value was created with
    my_id := id


    # check that this effect is the same as the currently installed effect of type
    # `mutate.this`
    #
    is_currently_installed =>
      match mutate.this.type.get_if_installed
        x mutate.this => my_id = x.id
        nil           => false


    # check that this effect is installed and replace it.
    #
    check_and_replace
    is
      if !is_currently_installed
        mpanic "*** invalid mutate for {mutate.this.type}"
      mutate.this.env.replace


    # is this element open, i.e., can it be mutated?
    #
    public open => id != u64 0


    # stop any further mutations of this element
    #
    public close is
      set id := 0


  # create a new mutable value with the given initial value and update the
  # 'mutate' effect in the current environment
  #
  public new (
    public T type,

    # initial value, will be updated by 'put' or 'infix <-'.
    mutable_value T
    ) : mutable_element,
        auto_unwrap T mutate
  is


    # read the current value of this mutable value.
    #
    # If this is open, check that the mutate effect this was created with is still
    # installed in the current environment.
    #
    public get ! mutate.this =>
      if open
        check_and_replace
      mutable_value


    # read the mutable value that is now immutable after it was closed for mutation.
    #
    public val
    pre
      safety: !open
    =>
      mutable_value


    # update mutable field with new value
    #
    # Check that the mutate effect this was created with is still
    # installed in the current environment.
    #
    public put (
      # the new value to be stored with 'h'
      to T)
      ! mutate.this
    pre
      safety: open
    =>
      check_and_replace
      set mutable_value := to


    # infix operator for put, OCaml/F#-style syntax
    #
    public infix <- (to T) => put to


    # update mutable field using a function of the old value
    #
    public update (
      # function calculcating the new value from the old value
      f T->T
      )
    =>
      put (f get)


    # creates a copy of the mutable field
    #
    public copy new T =>
      new T get


    # unwrap this mutable value
    #
    public unwrap ! mutate => get


    # returns `as_string` of the current value
    #
    public redef as_string => get.as_string


  # create a mutable array.
  #
  module:public array (
         # element type
         public T type,

         # length of the array to create
         public length i64,

         # contents of the array
         module data fuzion.sys.internal_array T,
         _ unit

        ) : container.Mutable_Array T mutate.this, mutable_element
  pre
    safety: (length ≥ 0) && (length ≤ data.length.as_i64)
  is

    # get element at given index i
    #
    public redef index [ ] (i i64) T
    =>
      data[i.as_i32]


    # set element at given index i to given value o
    #
    public set [ ] (i i64, o T) unit
    =>
      check_and_replace
      data[i.as_i32] := o


    # add an element at the end of this array
    #
    public add (o T) unit
    =>
      check_and_replace
      d := if (data.length.as_i64 > length) data
        else
          new_data := fuzion.sys.internal_array_init T (max 8 data.length*2)
          for i in indices do
            new_data[i.as_i32] := data[i.as_i32]
          new_data
      d[length.as_i32] := o
      set data := d
      set length := length+1


    # create immutable array from this
    #
    public redef as_array =>
      array T length.as_i32 (i -> data[i])


    # create a list from this array
    #
    public redef as_list =>
      # since array is mutable,
      # we first copy the elements
      # to an immutable array.
      as_array.as_list


    # initialize one-dimensional mutable array
    #
    public type.new
     (LM type : mutate,

      # length of the array to create
      length i64,

      # initial value for elements
      init T

     ) container.Mutable_Array T LM
    =>
      data := fuzion.sys.internal_array_init T length.as_i32

      for x in data.indices do
        data[x] := init

      LM.env.array T length data unit


    # initialize an empty mutable array of type T
    #
    public type.new (LM type : mutate) container.Mutable_Array T LM =>
      LM.env.array T 0 (fuzion.sys.internal_array_init T 0) unit



  # install default instance of mutate
  #
  module type.install_default =>
    mutate.default


# short-hand for accessing mut effect in current environment
#
public mut =>
  mutate.install_default
  mutate.env


# create a new mutable value of type T with initial value v
#
public mut(T type, v T) => mut.new T v
