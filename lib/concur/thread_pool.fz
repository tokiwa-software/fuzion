# This file is part of the Fuzion language implementation.
#
# The Fuzion language implementation is free software: you can redistribute it
# and/or modify it under the terms of the GNU General Public License as published
# by the Free Software Foundation, version 3 of the License.
#
# The Fuzion language implementation is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
# License for more details.
#
# You should have received a copy of the GNU General Public License along with The
# Fuzion language implementation.  If not, see <https://www.gnu.org/licenses/>.


# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion standard library feature concur.thread_pool
#
# -----------------------------------------------------------------------


# NYI: replace by better queue implementation, e.g. https://arxiv.org/pdf/2305.07229
Blocking_Queue(T type, mtx concur.sync.mutex, cnd concur.sync.condition) ref is

  # is closed for taking any new elements?
  #
  is_closed := concur.atomic false

  data := concur.atomic (array T) []


  # enqueue an element
  # if queue is already closed, returns false
  # otherwise enqueues the element and returns true
  #
  enqueue(el T) bool =>
    if is_closed.read
      false
    else
      mtx.synchronized ()->
        do
          old := data.read
          new := old.put old.length el
        until data.compare_and_set old new
        check cnd.broadcast
      true


  # dequeue an element
  # returns nil, if queue is closed and empty
  # otherwise returns the dequeued element
  #
  dequeue option T =>
    o := data.read
    if o.is_empty
      if is_closed.read
        nil
      else
        mtx.synchronized unit ()->
          if !is_closed.read
            # NYI: BUG: possible dead lock?
            # we should use TLA+ or similar to prove correctness
            # https://www.hillelwayne.com/post/list-of-tla-examples/
            check cnd.wait
        dequeue
    else
      res := o[0]
      n := array o.count-1 i->o[i+1]
      if data.compare_and_set o n
        res
      else
        dequeue


# simple, fixed size thread pool
#
# use `concur.thread_pool.env.submit` to
# submit a task to the thread_pool.
#
public thread_pool(R type, size i32, code ()->R) outcome R =>
  (concur.sync.mutex.new ()->
    mtx := concur.sync.mutex.env
    mtx.condition ()->
      cnd := concur.sync.condition.env
      (thread_pool mtx cnd size unit).instate_self ()->
        res := code.call
        thread_pool.env.await
        res)
    .bind R x->x



# simple, fixed size thread pool
#
private:public thread_pool(mtx concur.sync.mutex, cnd concur.sync.condition, size i32, _ unit) : effect
pre debug: size > 0
is

  bq := Blocking_Queue ()->unit mtx cnd

  threads := (1..size)
    .map _->
      concur.thread.spawn ()->
        _ := for x := bq.dequeue
             while !x.is_empty
               x.val.call
    .as_array


  # submit a task to the thread pool
  #
  # returns false if the pool is already shutting down
  #
  public submit(task ()->unit) bool =>
    bq.enqueue task


  # submit a task to the thread pool
  #
  # returns a future if successful or panics in case
  # the thread pool has already shut down.
  #
  public submit(T type, task ()->T) concur.Future T =>


    fut ref : concur.Future T is

      uid := unique_id

      compute_result := concur.atomic (option T) nil

      fut_cnd := concur.atomic (option fuzion.sys.Pointer) nil

      compute =>
        compute_result.write task.call
        mtx.synchronized ()->
          _ := fut_cnd.read.map_to_option c->
            check concur.sync.cnd_broadcast c
            concur.sync.cnd_destroy c

      redef is_done => compute_result.read.exists

      redef get T =>
        while !is_done
          mtx.synchronized ()->
            match concur.sync.cnd_init mtx.mtx
              e error => panic e.msg
              c fuzion.sys.Pointer =>
                  fut_cnd.write c
                  if !is_done
                    check concur.sync.cnd_wait c mtx.mtx

        compute_result.read.val

      redef and_then(T2 type, new_task T->T2) =>
        submit T2 ()->(new_task get)

    res := fut

    if bq.enqueue ()->res.compute
      res
    else
      panic "Thread pool has already shut down."


  # initiate shutdown of thread pool
  #
  shutdown unit =>
    bq.is_closed.write true


  # initiate shutdown, then wait for all added tasks to complete
  #
  await =>

    shutdown

    # wake up all threads
    mtx.synchronized unit ()->
      _ := cnd.broadcast

    # wait for all threads to finish
    threads.for_each t->
      t.join
