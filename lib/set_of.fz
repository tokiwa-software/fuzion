# This file is part of the Fuzion language implementation.
#
# The Fuzion language implementation is free software: you can redistribute it
# and/or modify it under the terms of the GNU General Public License as published
# by the Free Software Foundation, version 3 of the License.
#
# The Fuzion language implementation is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
# License for more details.
#
# You should have received a copy of the GNU General Public License along with The
# Fuzion language implementation.  If not, see <https://www.gnu.org/licenses/>.


# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion standard library feature set_of
#
#  Author: Fridtjof Siebert (siebert@tokiwa.software)
#
# -----------------------------------------------------------------------

# set_of_ordered -- routine to initialize a set from a Sequence of ordered elements
#
# This feature creates an instance of a Set.
#
set_of_ordered(K type : ordered K, vs Sequence K) Set K is ps_set vs


# set_of_hashable -- routine to initialize a set from a Sequence of hashable elements
#
# This feature creates an instance of a Set.
#
set_of_hashable(K type : hasHash K, vs (CTrie K unit) | (Sequence K)) Set K is

  private map := CTrie K unit
  match vs
    vss (Sequence K) =>
      vss.for_each (k -> map.add k unit)
    vsc (CTrie K unit) =>
      set map := vsc

  ref : Set K

    # does this set contain the given value?
    #
    contains (k K) bool is
      map[k].exists


    # list representation of values in this set
    #
    as_list list K is
      map.keys.as_list


    # add new element k to this set.
    #
    add (k K) Set K is
      ss := map.snapshot false
      ss.add k unit
      set_of_hashable ss


    # remove an element k from the set if it exists.
    # return the same set if it does not exist.
    #
    remove (k K) Set K is
      ss := map.snapshot false
      ss.remove k
      set_of_hashable ss
