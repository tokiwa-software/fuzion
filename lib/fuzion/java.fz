# This file is part of the Fuzion language implementation.
#
# The Fuzion language implementation is free software: you can redistribute it
# and/or modify it under the terms of the GNU General Public License as published
# by the Free Software Foundation, version 3 of the License.
#
# The Fuzion language implementation is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
# License for more details.
#
# You should have received a copy of the GNU General Public License along with The
# Fuzion language implementation.  If not, see <https://www.gnu.org/licenses/>.


# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion standard library feature fuzion.java
#
#  Author: Fridtjof Siebert (siebert@tokiwa.software)
#
# -----------------------------------------------------------------------

public fuzion.java is


  # read a static field of given name in class with given name.  Wrap result into
  # an instance of T.
  #
  public getStaticField<T>(clazz string, field string) =>
    c := stringToJavaObject clazz
    f := stringToJavaObject field
    getStaticField0<T> c f


  # read a field of given name from given object instance thiz.  Wrap result into
  # an instance of T.
  #
  public getField<T>(thiz JavaObject, field string) =>
    f := stringToJavaObject field
    getField0<T> thiz f


  # intrinsic to read static field
  #
  private getStaticField0<T>(clazz JavaObject, field JavaObject) T is intrinsic


  # intrinsic to read instance field
  #
  private getField0<T>(thiz JavaObject, field JavaObject) T is intrinsic


  # A Java reference
  #
  public JavaObject(private javaRef Object) ref
  is
    isNull bool is intrinsic


  # A Java array
  #
  public Array<T>(private redef javaRef Object) ref : Sequence<T>, JavaObject javaRef
  is
    length            => arrayLength<T> Array.this
    index [ ] (i i32) => arrayGet<T>    Array.this i
    redef asList => asList 0

    # create list starting at index from
    #
    asList (from i32) list<T> is
      if length <= from
        nil
      else
        arrayCons from

    # create a cons cell for a list of this array starting at the given
    # index
    #
    private arrayCons (i i32) : Cons<T, list<T>>
      pre
        debug: 0 <= i < length
    is
      head => Array.this[i]
      tail => asList i+1



  # Java's 'java.lang.String' type
  #
  public JavaString(private redef javaRef Object) ref : string, JavaObject javaRef
  is
    utf8 => (javaStringToString JavaString.this).utf8


  # Java's 'void' type (which is used in Java like Fuzion's 'unit' type').
  #
  public JavaVoid(forbiddenToCallDirectly void) : JavaObject(Object)
  is


  # Call a virtual Java method className.name with given signature signature
  # on instance thiz with given arguments
  #
  public callVirtual<T>(className string,
                        name string,
                        signature string,
                        thiz JavaObject,
                        args array<JavaObject>) T
  pre
    safety: !thiz.isNull
  is
    c := stringToJavaObject className
    n := stringToJavaObject name
    s := stringToJavaObject signature
    a := args.internalArray
    callV0<T> c n s thiz a


  # Call a static Java method with given name and signature with given
  # arguments
  #
  public callStatic<T>(className string,
                       name string,
                       signature string,
                       args array<JavaObject>) T
  is
    c := stringToJavaObject className
    n := stringToJavaObject name
    s := stringToJavaObject signature
    a := args.internalArray
    callS0<T> c n s a


  # Call a Java constructor with given signature for class with given name
  # with given arguments
  #
  public callConstructor<T> (className string,
                             signature string,
                             args array<JavaObject>) T
  is
    c := stringToJavaObject className
    s := stringToJavaObject signature
    a := args.internalArray
    callC0<T> c s a


  # intrinsic to call given Java instance method
  #
  private callV0<T>(className JavaObject,
                    name JavaObject,
                    signature JavaObject,
                    thiz JavaObject,
                    args sys.array<JavaObject>) T is intrinsic

  # intrinsic to call given Java static method
  #
  private callS0<T>(className JavaObject,
                    name JavaObject,
                    signature JavaObject,
                    args sys.array<JavaObject>) T is intrinsic


  # intrinsic to call given Java constructor
  #
  private callC0<T>(className JavaObject,
                    signature JavaObject,
                    args sys.array<JavaObject>) T is intrinsic


  # intrinsic to get the length of a Java array of arbitrary type
  #
  private arrayLength<T>(a Array<T>) i32 is intrinsic


  # intrinsic to get an element of a Java array of arbitrary type
  #
  private arrayGet<T>(a Array<T>, ix i32) T is intrinsic


  # convert a Sequence to a Java Array object
  #
  public arrayToJavaObject<T>(a Sequence<T>) Array<T> is
    arrayToJavaObject0<T> a.asArray.internalArray.data


  # intrinsic to convert an array to a Java Array object
  #
  private arrayToJavaObject0<T>(a Object) Array<T> is intrinsic


  # convert a string to a Java String object
  #
  public stringToJavaObject(s string) JavaObject is
    stringToJavaObject0 s.utf8.asArray.internalArray.data


  # intrinsic to convert a string to a Java String object
  #
  private stringToJavaObject0(s Object) JavaObject is intrinsic


  # convert a Java String to a stirng
  #
  private javaStringToString(js JavaString) string is intrinsic


  # intrinsic to convert an i8 to a Java Byte object
  #
  public i8ToJavaObject(v i8) JavaObject is intrinsic


  # intrinsic to convert an u16 to a Java Character object
  #
  public u16ToJavaObject(v u16) JavaObject is intrinsic


  # intrinsic to convert an i16 to a Java Short object
  #
  public i16ToJavaObject(v i16) JavaObject is intrinsic


  # intrinsic to convert an i32 to a Java Integer object
  #
  public i32ToJavaObject(v i32) JavaObject is intrinsic


  # intrinsic to convert an i64 to a Java Long object
  #
  public i64ToJavaObject(v i64) JavaObject is intrinsic


  # intrinsic to convert an f32 to a Java Float object
  #
  public f32ToJavaObject(v f32) JavaObject is intrinsic


  # intrinsic to convert a f64 to a Java Double object
  #
  public f64ToJavaObject(v f64) JavaObject is intrinsic


  # intrinsic to convert a bool to a Java Boolean object
  #
  public boolToJavaObject(b bool) JavaObject is intrinsic
