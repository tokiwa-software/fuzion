# This file is part of the Fuzion language implementation.
#
# The Fuzion language implementation is free software: you can redistribute it
# and/or modify it under the terms of the GNU General Public License as published
# by the Free Software Foundation, version 3 of the License.
#
# The Fuzion language implementation is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
# License for more details.
#
# You should have received a copy of the GNU General Public License along with The
# Fuzion language implementation.  If not, see <https://www.gnu.org/licenses/>.


# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion test mod_webserver
#
# -----------------------------------------------------------------------

mod_webserver =>

  started := concur.atomic bool .new false

  cnt := concur.atomic i32 .new 0

  ows :=
    webserver_create
      9999
      net.family.ipv4
      (x -> if x.starts_with "listening on port:" then started.write true else unit)
      req->
        _ := mutate
        _ := cnt.incr

        # inject fault in 4th request, causes 500 response
        if cnt.read = 4
          fuzion.runtime.contract_fault.cause ("some fault", "___")

        resp := "Pong"
        http
          .response_message
          .new
            200
            [("Server", "FuzionWebServer v0.0.0"),
             ("Content-Length", resp.byte_length.as_string),
             ("Host", "localhost")]
            (io.Read_Handler.from mutate resp)


  match ows
    e error => say e
    ws Webserver =>
      # busy loop
      while !started.read do

      for i in 1..10 do
        say <| web.get4 "http://localhost:9999/"

      ws.shutdown

      # one more request after shutdown to unblock the accept
      say <| web.get4 "http://localhost:9999/"
