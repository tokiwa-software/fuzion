# This file is part of the Fuzion language implementation.
#
# The Fuzion language implementation is free software: you can redistribute it
# and/or modify it under the terms of the GNU General Public License as published
# by the Free Software Foundation, version 3 of the License.
#
# The Fuzion language implementation is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
# License for more details.
#
# You should have received a copy of the GNU General Public License along with The
# Fuzion language implementation.  If not, see <https://www.gnu.org/licenses/>.


# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion test data_size_test
#
# -----------------------------------------------------------------------

data_size_test =>

  x := units.data_size.tb 1
  say "A {x.as_string_si} HDD is only $x."

  max_val := units.data_size u128.max
  say "The highest possible units.data_size is $max_val (base 2) or {max_val.as_string_si} (base 10)."
  check max_val = units.data_size.max

  val := units.data_size.kib 200
  mult := 8.as_u128
  val_mult := val * mult
  say "$val times $mult is $val_mult or {val_mult.as_string units.byte_unit_iec.kib}."
  say ""


  one_b := units.data_size.b 1
  two_b := units.data_size.b 2

  show(b u128, d units.data_size, iec bool) =>
    say "{b.as_string.pad_left 35} B: {iec ? d.as_string_pad : d.as_string_si_pad}"
    check b = d.bytes

  # test a data_size as well as its nearest two on both sides
  test(n u128, ds units.data_size, iec bool) =>
      if n >= 2.as_u128
        show (n-2.as_u128) (ds - two_b) iec
      if n >= 1.as_u128
        show (n-1.as_u128) (ds - one_b) iec
      show    n             ds          iec
      show   (n+1.as_u128) (ds + one_b) iec
      show   (n+2.as_u128) (ds + two_b) iec

  # test decimal (si) prefixes where they change from one unit to the next
  # as well as where they change the number of displayed decimals
  for i in 0..33 do
    n := 10.as_u128 ** i.as_u128
    ds := units.data_size n
    test n ds false

  say ""

  # test binary (iec) prefixes
  # transition between units as well as values with two and three digits
  for i in 0..11 do
    for j in [0, 3, 6] do
      n := 2.as_u128 ** (i*10 + j).as_u128
      ds := units.data_size n

      test n ds true
