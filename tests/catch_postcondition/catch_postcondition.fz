# This file is part of the Fuzion language implementation.
#
# The Fuzion language implementation is free software: you can redistribute it
# and/or modify it under the terms of the GNU General Public License as published
# by the Free Software Foundation, version 3 of the License.
#
# The Fuzion language implementation is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
# License for more details.
#
# You should have received a copy of the GNU General Public License along with The
# Fuzion language implementation.  If not, see <https://www.gnu.org/licenses/>.


# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion test catch_postcondition
#
#  Author: Fridtjof Siebert (siebert@tokiwa.software)
#
# -----------------------------------------------------------------------

catch_postcondition is


  # test feature will cause postcondition failure if called with types
  # smaller than 64 bits:
  #
  test(T type : integer) =>

    # duplicate x
    #
    double(x T)
      post
        equals result-x x
    =>
      x +^ x

    yak "trying $T... "
    for v := T.one, double v
        i in 0..50
    else
      $v


  # -------------------------------

  /*

    Java code to explain what we are trying to do in Fuzion here:

        String res;
        try
          {
            res = test<i32>();
          }
        catch (PostConditionError e)
          {
            System.out.println("*** failed: "+e+" ***");
            res = test<u64>();
          }
        System.out.println(res);

    The following are several attempts to find a useful syntax for Fuzion

  */

  # -------------------------------

  # alias for brevity
  #
  rt := fuzion.runtime

  # -------------------------------

  say "install our own fault handler that prints message and tries again:"

  my_hdlr : fuzion.runtime.Post_Fault_Handler is
    cause(msg String) =>
      say "postcondition failed: $msg"
      rt.post_fault.abort

  say ((rt.post_fault my_hdlr).run
    (test i32)
    (test u64))

  say ""

  # -------------------------------

  # convenience feature to install post_fault handler:
  #
  handle_post(T type) is

    # try and catch to be redefined by user
    #
    try               T => abstract
    catch(msg String) T => abstract

    # use local mutate to store msg passed to `cause`
    M : mutate.

    res := M.go ()->
        m := M.env.new (option String nil)
        h : fuzion.runtime.Post_Fault_Handler is
          cause(msg String) =>
            m <- msg
            rt.post_fault.return
        (rt.post_fault h).run try (()->catch m.get.get)


  # -------------------------------

  say "using convenience feature with `try` and `catch`"

  y : handle_post String is
    try             => test i32
    catch(s String) => say "*** failed: $s ***"; test u64

  say y.res

  say ""

  # -------------------------------

  say "using anonymous instance of convenience feature with `try` and `catch`"

  say (ref : handle_post String
    try =>
      test i32
    catch(s String) =>
      say "*** failed: $s ***"
      test u64
    ).res

  say ""

  # -------------------------------

  say "using convenience feature for repeated `try` and `catch` using mutable var and recursion"

  tries array ()->String := [ (() -> test i32), (() -> test i16), (() -> test i64) ]

  c := mut 0

  z : handle_post (option String) is
    try option String  => tries[c].call
    catch(s String) option String => say "*** failed: $s ***"; c <- c.get + 1; if c.get < tries.count then z.res else nil

  say z.res

  say ""

  # -------------------------------

  say "using convenience feature for repeated `try` and `catch` using loop"

  for c in tries.indices do
    h : handle_post (option String) is
      try option String  => tries[c].call
      catch(s String) option String => say "*** failed: $s ***"; nil
    r := h.res
  until r??
    say r
  else
    say "*** all failed ***"

  say ""

  # -------------------------------

  say "using default post handler, will crash:"

  say (test i32)
  say (test u64)  # not reached!
