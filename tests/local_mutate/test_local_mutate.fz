# This file is part of the Fuzion language implementation.
#
# The Fuzion language implementation is free software: you can redistribute it
# and/or modify it under the terms of the GNU General Public License as published
# by the Free Software Foundation, version 3 of the License.
#
# The Fuzion language implementation is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
# License for more details.
#
# You should have received a copy of the GNU General Public License along with The
# Fuzion language implementation.  If not, see <https://www.gnu.org/licenses/>.


# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion test test_local_mutate
#
#  Author: Fridtjof Siebert (siebert@tokiwa.software)
#
# -----------------------------------------------------------------------

# a test for creating and installing a mutate effect and then using this to create
# some data that is then returned as an immutable object
#
test_local_mutate is

  # the first test uses a local variable to count the elements in a Sequence:
  #
  test_sum =>

    # our sequence to count:
    #
    l0 := [0,8,15,47,11]

    sum(l Sequence i32) =>

      # define a local type `m` to identify the mutate effect:
      #
      m : mutate is

      # count using a locally mutable variable:
      #
      count =>

        # create mutable variable s:
        s := m.env.new 0

        for e in l do

          # add e to s
          s.update a->a+e

        # unwrap the sum value in s and return it
        s.get

      # run code within an instance of m
      #
      m.instate_self ()->
        s := m.env.new 0

        for e in l do
          s.update a->a+e

        say "inside m.go: s.get = {s.get}"
        say "inside m.go: count = {count}"

      say "using m.instate_self and count: {m.instate_self ()->count}"

      count2 =>
        s := m.env.new 0
        for e in l do
          s.update a->a+e
        option s

      # This was moved to test_local_mutate_neg
      #
      # say count    # *** will cause compile-time an error, requires m to be installed

      neg_test3 =>
        s := m.instate_self ()->count2
        say s             # *** will cause an error, requires m to be installed
        "** failed, did not panic **"

      caught_msg(msg) => "*** got '$msg' ***\nok"

      if false       # NYI: BUG #3352: this does not work for the C backend
        say (panic.try   neg_test3
                  .catch caught_msg)

      say (panic.try   neg_test4
                .catch caught_msg)

      neg_test4 =>
        s := m.instate_self ()->count2
        m.instate_self ()->
          s.get <- -12    # *** will cause an error, m has changed!
          say s
        "** failed, did not panic **"

      say (panic.try   neg_test5
                .catch caught_msg)

      neg_test5 =>
        q =>
          s1 := m.instate_self ()->count2
          m.instate_self ()->
            s1.get <- -12    # *** will cause an error, m has changed!
            say s1
          "** failed, did not panic **"
        q

    sum l0

  test_sum


  # quicksort using array created via local mutate effect
  #
  test_quicksort =>

    quicksort(T type: property.orderable, data Sequence T) array T =>
      c := data.count
      if c = 0
        []
      else
        m : mutate is

        # execute code with mutate effect `m`:
        m.instate_self ()->

          arr := (mutate.array T).new m c.as_i64 data[0]

          swap(i,j i32) =>
            t := arr[i.as_i64]
            arr[i.as_i64] := arr[j.as_i64]
            arr[j.as_i64] := t

          partition(l, r, pivot i32) =>
            pv := arr[pivot.as_i64]
            swap pivot r
            for
              a := l, a + (sw ? 0 : 1)
              b := r, b - (sw ? 1 : 0)
              sw := arr[a.as_i64] > pv
            while a < b
              if sw
                swap a b-1
            else
              swap b r
              b

          qs(l, r i32) unit => # =>  does not work
            if l < r
              pi := partition l r (l + r)/2
              qs l pi-1
              qs pi+1 r

          for i in arr.indices do
            arr[i] := data[i.as_i32]

          qs 0 c-1
          arr.as_array


    say (quicksort [0, 8, 15, 47, 11])
    for i in -1..10 do
      say (quicksort i32 0..i)
      say (quicksort i32 (i..0 : -1))

  test_quicksort


  # first try to privde a doubly-linked ring
  #
  test_ring1 =>

    Cell(T type, LM type : mutate, data T) ref is
      n := LM.env.new (Cell T LM) Cell.this
      p := LM.env.new (Cell T LM) Cell.this
      link(ne, pr Cell T LM) =>
        n <- ne
        p <- pr

      next => n.get
      prev => p.get

    ring(T type, from0 Sequence T)
    pre
      !from0.is_empty
    =>
      m : mutate is

      ring2(from list T) Cell T m =>   # NYI '=>' causes error cyclic type inference
        c := Cell T m from.head.get
        match from.tail
          nil    => _ := c
          t Cons => f := ring2 t
                    l := f.p.get
                    c.link f l
                    f.link f.n.get c
                    l.link c l.p.get
        c

      ring3(from list T) Cell T m =>
        r := ring2 from

        for
          i in 1..10
          e := r, e.n.get
        do
          say "$i: {e.data}"

        _ := for cur := r, cur.next
             while cur.n.open do
               cur.n.close
               cur.p.close
        r

      r0 := m.instate_self ()->(ring3 from0.as_list)
      say "left mutate environment m"
      for
        i in 1..10
        e := r0, e.next
      do
        say "$i: {e.data}"
      r0

    _ := ring [0,1,2,3]

  test_ring1


  # second try to privde a doubly-linked ring and to return it as a type
  # 'ring' that does not refer to any mutate effect:
  #
  test_ring2 =>

    # abstract type for 'ring'
    #
    Ring(T type) ref is
      next, prev Ring T => abstract
      data T => abstract

      redef as_string =>
        for
          s := "[ ", s + "{e.data} <-> "
          # e2 := r, e2.next    # NYI causes crash!
          e Ring T := Ring.this, e.next
          i in 0..3
        else
          s + " ... ]"

    # ring type for mutable rings
    #
    # on call to `Mut_Ring LM T data` creates a minimal ring consisting of only
    # one single cell.  To create larger rings, you can either call `insert` to
    # add single cells, or `merge` to merge two rings into one larger ring.
    #
    Mut_Ring(# mutate effect to be used to create mutable variables
             LM type : mutate,

             # type of data stored in ring cells
             T type,

             # the data stored in this cell.
             redef data T) ref : Ring T is


      # mutable references to next and previous. Initializes to refer to
      # Mut_Ring.this to form a minimal ring
      #
      private n := LM.env.new (Mut_Ring LM T) Mut_Ring.this
      private p := LM.env.new (Mut_Ring LM T) Mut_Ring.this


      # short-hand features to get the mutable references from `n` and `p`
      #
      redef next => n.get
      redef prev => p.get


      # add a cell containing `data` to this ring.
      #
      # Mut_Ring.this is 'a':
      #
      #  +-> a <-> b <-> c ... m <-+
      #  |                         |
      #  +-------------------------+
      #
      # the resulting ring then will have a new cell `n` containing `data`
      # with this shape:
      #
      #  +-> a <-> b <-> c ... m <-> n <-+
      #  |                               |
      #  +-------------------------------+
      #
      insert(data0 T) =>
        r2 := Mut_Ring LM T data0
        merge r2


      # merge two rings as follows:
      #
      # Mut_Ring.this is 'a':
      #
      #  +-> a <-> b <-> c ... m <-+
      #  |                         |
      #  +-------------------------+
      #
      # the second ring `n` is
      #
      #  +-> n <-> o <-> p ... z <-+
      #  |                         |
      #  +-------------------------+
      #
      # the resulting ring then will be:
      #
      #  +-> a <-> b <-> c ... m <-> n <-> o <-> p ... z <-+
      #  |                                                 |
      #  +-------------------------------------------------+
      #
      merge(n0 Mut_Ring LM T) =>
        a := Mut_Ring.this
        m := a.p.get
        z := n0.n.get
        a.p <- z
        z.n <- a
        m.n <- n0
        n0.p <- m

      # freeze this ring, i.e., turn all references into immutable values
      #
      freeze =>
        if n.open
          n.close
          p.close
          next.freeze

    # create a ring from the elements of a sequence
    #
    create_ring(T type, from0 Sequence T)
    pre
      !from0.is_empty
    =>
      # define mutate context to be used
      m : mutate is

      # create ring, called with the env set to an instance of `m`.
      #
      create_ring2(from list T) Ring T =>
        c := Mut_Ring m T from.head.get
        from.as_list.tail.for_each (x->c.merge (Mut_Ring m T x))
        c.freeze
        c

      r := m.instate_self ()->(create_ring2 from0.as_list)
      say "left mutate environment m"
      say r
      for
        i in 1..10
        e := r, e.next
        b := r, b.prev
      do
        say "$i: {e.data} {b.data}"
      r

    _ := create_ring [0,1,2,3]

  test_ring2
