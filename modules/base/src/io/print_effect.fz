# This file is part of the Fuzion language implementation.
#
# The Fuzion language implementation is free software: you can redistribute it
# and/or modify it under the terms of the GNU General Public License as published
# by the Free Software Foundation, version 3 of the License.
#
# The Fuzion language implementation is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
# License for more details.
#
# You should have received a copy of the GNU General Public License along with The
# Fuzion language implementation.  If not, see <https://www.gnu.org/licenses/>.


# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion standard library feature print_effect
#
#  Author: Fridtjof Siebert (siebert@tokiwa.software)
#
# -----------------------------------------------------------------------

# print_effect -- effect providing an output stream
#
# This is used as heir feature for effects such as io.Out and io.Err.
#
public print_effect : effect
is

  # print the given string followed be new line
  #
  public println (s Any) unit =>
    print (s.as_string + (codepoint 10))


  # print the given string
  #
  public print (s Any) unit =>
    # NYI: BUG: error handling, maybe we should raise
    # some kind of IO runtime fault here?
    if s.as_string.utf8.count > os.page_size.as_i32
      for c in s.as_string.utf8.chunk os.page_size.as_i32
      do
        write_res := write c
        check debug: write_res.ok
    else
      write_res := write s.as_string.utf8
      check debug: write_res.ok
    flush_res := flush
    check flush_res.ok


  # print new line
  #
  public println unit =>
    print (codepoint 10)


  # write bytes (without explicit flushing)
  #
  public write(b Sequence u8) outcome unit
    pre debug: b.count <= os.page_size.as_i32
  => abstract


  # explicitly flush everything that has been written
  #
  public flush outcome unit => abstract
