# This file is part of the Fuzion language implementation.
#
# The Fuzion language implementation is free software: you can redistribute it
# and/or modify it under the terms of the GNU General Public License as published
# by the Free Software Foundation, version 3 of the License.
#
# The Fuzion language implementation is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
# License for more details.
#
# You should have received a copy of the GNU General Public License along with The
# Fuzion language implementation.  If not, see <https://www.gnu.org/licenses/>.


# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion standard library feature exception
#
#  Author: Fridtjof Siebert (siebert@tokiwa.software)
#
# -----------------------------------------------------------------------

# exception -- simple exception effect
#
# exception provides an operation 'cause' that immediately stops execution and
# returns an 'error' wrapped in an 'outcome'.
#
public exception (# type parameter used only to distinguish different
                  # instances of `exception`. This permits using, e.g.,
                  # `exception reader` and `exception writer` and distinguish the
                  # errors raised by the reader from those raised
                  # by the writer.
                  #
                  public T type,

                  h error->void
                  ) : eff.fallible error h
is

  # type feature that to create an instance of `exception` with the given error handler.
  #
  public fixed redef type.new(h error->void) =>
    exception T h


  # install this effect and execute 'f'. Wrap the result of 'f' into an
  # 'outcome' if 'f' returns normally, otherwise if 'f' is aborted early
  # via a call to 'raise' wrap the 'error' passed to 'raise' into the
  # resulting 'outcome'.
  #
  public type.on(R type, f ()->R) outcome R =>
    exception.this.try   (outcome R) ()->
                    f()
                  .catch
                    id


# convenience routine to create a new instance of 'exception T' and run 'f' in
# it.  The result will be an `outcome R` that, in case of success, contains
# the result of `f`, or, in case of `(exception T).env.raise`, contains the error.
#
public exception(T type,
           R type,
           f ()->R) =>
  (exception T).on f


# convenience routine to create a new instance of 'exception T' and run 'f' in
# it. Return the result of 'f' directly or panic in case 'f' calls
# '(exception T).env.raise'.
#
public exception_or_panic(T type,
                          R type,
                          f ()->R) R =>
  match (exception T).on f
    e error => panic $e
    r R => r
