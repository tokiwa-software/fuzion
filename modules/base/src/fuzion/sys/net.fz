# This file is part of the Fuzion language implementation.
#
# The Fuzion language implementation is free software: you can redistribute it
# and/or modify it under the terms of the GNU General Public License as published
# by the Free Software Foundation, version 3 of the License.
#
# The Fuzion language implementation is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
# License for more details.
#
# You should have received a copy of the GNU General Public License along with The
# Fuzion language implementation.  If not, see <https://www.gnu.org/licenses/>.


# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion standard library feature fuzion.sys.net0
#
# -----------------------------------------------------------------------

# groups networking related features
#
# internally this builds on:
# - java: nio
#   - why not java.net.Socket etc.?: this is only suitable for blocking I/O
#   - why not nio2?                : nio2 is basically the same as nio but asynchronous.
#                                    I.e. read/write return Futures.
# - c/posix: posix-sockets
# - c/win: winsock2
#
# sources of inspiration:
# - https://github.com/tezc/sc/
# - https://github.com/jart/cosmopolitan
# - https://learn.microsoft.com/en-us/windows/win32/winsock/complete-server-code
# - https://learn.microsoft.com/en-us/windows/win32/winsock/complete-client-code
#
# NYI: use error codes coming from backend
#
module net is


  # bind a name to a newly created socket
  #
  # 0  => arr_result[0] is the socket descriptor
  # -1 => arr_result[0] is an error number
  #
  fzE_bind (family, socket_type, protocol i32, host fuzion.sys.Pointer, port fuzion.sys.Pointer, arr_result fuzion.sys.Pointer) i32 => native


  # bind a name to a newly created socket, wrapper for fzE_bind native
  #
  module bind    (family, socket_type, protocol i32, host String, port u16) outcome i32 =>
    arr := fuzion.sys.internal_array_init i32 1
    r := fzE_bind family socket_type protocol (fuzion.sys.c_string host) (fuzion.sys.c_string port.as_string) arr.data = 0
    if r
      arr[0]
    else
      error "error: {arr[0]}"



  # activates a server socket, setting a backlog
  # of a maximum amount of connections which are kept
  # waiting for acceptance.
  #
  # returns zero on success, anything else is an error.
  #
  module fzE_listen(sd i32, backlog i32) i32 => native


  # accept a new connection for given socket descriptor.
  # may block until there is a connection to accept.
  # returns a new / different descriptor which
  # corresponds to the accepted connection only.
  #
  fzE_accept(sd i32) i32 => native


  # accept a new connection for given socket descriptor, wrapper for accept native.
  # returns an error or a new descriptor.
  module accept(sd i32) outcome i32 =>
    desc := fzE_accept sd
    if desc != -1 then desc
    else error "accept failed, error number: {desc}"


  # open and connect a client socket
  #
  # 0  => arr_result[0] is the socket descriptor
  # -1 => arr_result[0] is an error number
  #
  fzE_connect(family, socket_type, protocol i32, host fuzion.sys.Pointer, port fuzion.sys.Pointer, arr_result fuzion.sys.Pointer) i32 => native


  # open and connect a client socket
  #
  module connect(family, socket_type, protocol i32, host String, port u16) outcome i32 =>
    arr := fuzion.sys.internal_array_init i32 1
    res := fzE_connect family socket_type protocol (fuzion.sys.c_string host) (fuzion.sys.c_string port.as_string) arr.data = 0
    if res
      arr[0]
    else
      error "error: {arr[0]}"



  # read bytes into arr_data buffer
  #
  # true  => arr_result[0] is the number of bytes read
  # false => arr_result[0] is an error number
  #
  fzE_read(sd i32, arr_data fuzion.sys.Pointer, length i64) i32 => native


  # read a maximum of max_bytes from descriptor, wrapper for read native
  # may block if descriptor is set to blocking.
  #
  module read(descriptor i32, max_bytes i64) outcome (array u8) =>
    buff := fuzion.sys.internal_array_init u8 max_bytes.as_i32
    res := fzE_read descriptor buff.data max_bytes
    if res >= 0
      if res.as_i64 = max_bytes
        buff.as_array
      else
        # NYI there should be a way to use a slice of internal_array to init array
        array u8 res (idx -> buff[idx])
    else
      error "error reading from socket: {res}"


  # write buffer bytes on socket
  #
  # returns zero on success, anything else is an error.
  #
  fzE_write(sd i32, arr_data fuzion.sys.Pointer, length i64) i32 => native


  # write data to descriptor, wrapper for write native
  # may block if descriptor is set to blocking.
  #
  module write(descriptor i32, data array u8) outcome unit =>
    res := fzE_write descriptor data.internal_array.data data.length.as_i64
    if res = -1 then error "error: $res" else unit


  # close socket
  #
  # returns zero on success, anything else is an error.
  #
  fzE_close(sd i32) i32 => native


  # close socket descriptor
  module close(sd i32) outcome unit =>
    res := fzE_close sd
    if res = -1 then error "error: $res" else unit


  # 0 = blocking, 1 = none_blocking
  #
  # returns zero on success, anything else is an error.
  #
  # NYI non blocking needs some kind of polling mechanism like epoll / kqueue
  # probably not good enough:
  # - select "can monitor only file descriptors  numbers
  #           that  are less  than  FD_SETSIZE (1024)â€”an unreasonably
  #           low limit for many modern applications"
  # - poll is in O(n)
  # difficult to implement on windows, read here: https://notgull.github.io/device-afd/
  fzE_set_blocking(sd i32, blocking i32) i32 => native


  # set descriptor to blocking / none blocking mode.
  module set_blocking(sd i32, blocking bool) outcome unit =>
    res := if blocking then fzE_set_blocking sd 0 else fzE_set_blocking sd 1
    if res = 0 then unit else error "error: $res"


  # get a socket's peer's ip address
  #
  # takes a socket descriptor number and an array of 16 bytes in
  # which the IP address will be stored
  #
  # returns the length of the IP address written to the array,
  # in bytes (4 for IPv4, 16 for IPv6)
  #
  fzE_get_peer_address(sockfd i32, address fuzion.sys.Pointer) i32 => native


  # get a socket's peer's ip address
  #
  # takes a socket descriptor number
  # returns the IP address, as a list of bytes
  #
  # not useful for UDP sockets (information not necessarily available)
  #
  module get_peer_address(sockfd i32) outcome (list u8) =>
    iarr := fuzion.sys.internal_array_init u8 16
    l := fzE_get_peer_address sockfd iarr.data
    iarr.as_array.take l


  # get a socket's peer's port
  #
  # takes a socket descriptor number
  # returns the port number
  #
  # not useful for UDP sockets (information not necessarily available)
  #
  module fzE_get_peer_port(sockfd i32) u16 => native
