# This file is part of the Fuzion language implementation.
#
# The Fuzion language implementation is free software: you can redistribute it
# and/or modify it under the terms of the GNU General Public License as published
# by the Free Software Foundation, version 3 of the License.
#
# The Fuzion language implementation is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
# License for more details.
#
# You should have received a copy of the GNU General Public License along with The
# Fuzion language implementation.  If not, see <https://www.gnu.org/licenses/>.


# nom.multi -- parsers applied multiple times
#
module multi is

  # apply parser multiple times, return results as sequence
  #
  public many0 (I, O type, p Parser I I O) =>
    parse(input I) parse_result I (Sequence O) =>
      match p.call input
        s1 success I O =>
          match (parse s1.rest)
            s2 success I (Sequence O) => success s2.rest [s1.out]++s2.out
            error => panic "invalid state, many0 must always succeed."
        # NYI: UNDER DEVELOPMENT: type inference should infer correct type of empty array
        error => success input (Sequence O).type.empty
    parser I _ _ (input -> parse input)


  # apply parser at least once, return results as sequence
  #
  public many1 (I, O type, p Parser I I O) =>
    parser I I (Sequence O) input->
      p.call input .bind _->
        (many0 p).call input


  # apply parser between m and n times, return results as sequence
  #
  public many_m_n (I, O type, m, n i32, p Parser I I O)
  pre m<=n
      n>0
  =>
    parser I I (Sequence O) input->
      rest, acc := (1..n)
        .reduce (input, (Sequence O).type.empty) r,t->
          match p.call r.0
            s success =>
              (s.rest, r.1 ++ [s.out])
            error =>
              abort (r.0, r.1)

      if acc.count < m
        error "matched only {acc.count} times"
      else
        success rest acc


  # parse a separated list of values, zero or more values
  #
  public separated_list0(I, O, D type, p_sep Parser I I D, p_value Parser I I O) =>
    parse(input I) parse_result I (Sequence O) =>
      match p_value.call input
        s1 success =>
          match (many0 (sequence.preceded p_sep p_value)).call s1.rest
            s2 success => success s2.rest [s1.out]++s2.out
            error      => panic "invalid state, many0 always succeeds"
        # NYI: UNDER DEVELOPMENT: type inference should infer correct type of empty array
        error => success input (Sequence O).type.empty
    parser I _ _ (input -> parse input)


  # parse a separated list of values, one or more values
  #
  public separated_list1(I, O, D type, p_sep Parser I I D, p_value () -> (Parser I I O)) =>
    parse(input I) parse_result I (Sequence O) =>
      p_value().call input .bind s1->
        match (many0 (sequence.preceded p_sep p_value())).call s1.rest
          s2 success => id (parse_result I (Sequence O)) (success s2.rest [s1.out]++s2.out)
          error => panic "invalid state, many0 always succeeds"
    parser I _ _ (input -> parse input)
