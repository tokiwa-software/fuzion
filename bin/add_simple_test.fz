# This file is part of the Fuzion language implementation.
#
# The Fuzion language implementation is free software: you can redistribute it
# and/or modify it under the terms of the GNU General Public License as published
# by the Free Software Foundation, version 3 of the License.
#
# The Fuzion language implementation is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
# License for more details.
#
# You should have received a copy of the GNU General Public License along with The
# Fuzion language implementation.  If not, see <https://www.gnu.org/licenses/>.


# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of utility add_simple_test
#
# -----------------------------------------------------------------------

# convenience utility for adding a new simple test
#
add_simple_test =>

  say "Name of the new test: "

  lm : mutate is
  lm.instate_self ()->

    _ := (io.stdin lm).try void ()->
      match io.buffered.read_line lm
        test String =>

          header(name String) => """
            # This file is part of the Fuzion language implementation.
            #
            # The Fuzion language implementation is free software: you can redistribute it
            # and/or modify it under the terms of the GNU General Public License as published
            # by the Free Software Foundation, version 3 of the License.
            #
            # The Fuzion language implementation is distributed in the hope that it will be
            # useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
            # MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
            # License for more details.
            #
            # You should have received a copy of the GNU General Public License along with The
            # Fuzion language implementation.  If not, see <https://www.gnu.org/licenses/>.


            # -----------------------------------------------------------------------
            #
            #  Tokiwa Software GmbH, Germany
            #
            #  Source code of Fuzion test {name}
            #
            # -----------------------------------------------------------------------

          """

          makefile_body(name String) =>
            """
            override NAME = {name}
            include ../simple.mk
            """

          test_body(name String) =>
            """
            {name} =>

            """

          if test.contains_whitespace
            say "name contains whitespace"
            exit 1
          if test.contains "/" || test.contains "\\"
            say "name contains slash"
            exit 1
          if io.file.exists "tests/$test"
            say "$test already exists."
            exit 1

          _ := io.dir.make "tests/$test"
          _ := io.file.use unit lm "tests/$test/$test.fz" io.file.mode.write ()->
                ((io.buffered.writer lm).write ((header test)+(test_body test)).utf8).error
          _ := io.file.use unit lm "tests/$test/Makefile" io.file.mode.write ()->
                ((io.buffered.writer lm).write ((header "Makefile")+(makefile_body test)).utf8).error

          # NYI: UNDER DEVELOPMENT: workaround for reading uninitialized field
          exit 0
        io.end_of_file => exit 1




